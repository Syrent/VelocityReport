plugins {
    id 'java'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm' version '1.9.21'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.1.7'
}

group = 'ir.syrent.velocityreport'
version = findProperty("version")

repositories {
    mavenLocal()
    mavenCentral()

    maven {
        url = uri('https://repo.maven.apache.org/maven2/')
    }

    // Velocity-API / PaperLib
    maven {
        name 'papermc'
        url 'https://repo.papermc.io/repository/maven-public/'
    }

    // AdventureAPI/MiniMessage
    maven {
        name = "sonatype-oss-snapshots"
        url = "https://oss.sonatype.org/content/repositories/snapshots/"
    }

    // SpigotAPI
    maven {
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'

        // As of Gradle 5.1, you can limit this to only those
        // dependencies you expect from it
        content {
            includeGroup 'org.bukkit'
            includeGroup 'org.spigotmc'
        }
    }

    // VRuom
    /*maven {
        url = uri("https://maven.pkg.github.com/mohamad82bz/ruom")
        credentials {
            username = project.findProperty("gpr.user")
            password = project.findProperty("gpr.key")
        }
    }*/

    // PlaceholderAPI
    maven {
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }

    // Update checker
    maven {
        name = "jeff-media-public"
        url = 'https://hub.jeff-media.com/nexus/repository/jeff-media-public/'
    }
}

dependencies {
    compileOnly 'me.clip:placeholderapi:2.11.5'
    compileOnly 'com.velocitypowered:velocity-api:3.1.1'
    compileOnly 'org.spigotmc:spigot-api:1.20.1-R0.1-SNAPSHOT'

    compileOnly files('libs/VelocityVanish-3.23.1.jar')

    // gson is not exist in legacy minecraft version
    implementation 'com.google.code.gson:gson:2.10.1'

    implementation 'com.zaxxer:HikariCP:5.1.0'
    implementation 'io.papermc:paperlib:1.0.8'
    implementation 'org.bstats:bstats-bukkit:3.0.2'
//    implementation 'me.mohamad82:ruom-velocity:4.2.14'
    implementation 'club.minnced:discord-webhooks:0.8.4'
    implementation 'com.github.cryptomorin:XSeries:9.7.0'

    implementation 'net.kyori:adventure-api:4.14.0'
    implementation 'net.kyori:adventure-platform-bukkit:4.3.1'
    implementation 'net.kyori:adventure-text-minimessage:4.14.0'

    implementation 'com.jeff_media:SpigotUpdateChecker:3.0.3'

    annotationProcessor 'com.velocitypowered:velocity-api:3.1.1'
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    minimize()

    manifest {
        attributes["Main-Class"] = 'ir.syrent.velocityreport.VelocityReport'
    }

//    relocate('me.mohamad82.ruom', 'ir.syrent.velocityreport.ruom')
    relocate('com.cryptomorin.xseries', 'ir.syrent.velocityreport.xseries')
    relocate('net.kyori.adventure', 'ir.syrent.velocityreport.kyori.adventure')
    relocate('org.bstats', 'ir.syrent.velocityreport.bstats')
    relocate('kotlin', 'ir.syrent.velocityreport.kotlin')
    relocate('com.jeff_media.updatechecker', 'ir.syrent.velocityreport.updatechecker')
    relocate('com.google.gson', 'ir.syrent.velocityreport.google.gson')
    relocate('com.zaxxer.hikari', 'ir.syrent.velocityreport.zaaxer.hikari')

    // @see https://youtrack.jetbrains.com/issue/KT-25709
    exclude '**/*.kotlin_metadata'
    exclude '**/*.kotlin_module'
    exclude '**/*.kotlin_builtins'

    archiveFileName = findProperty("plugin-name") + " v" + findProperty("version") + ".jar"
}

jar {
    archiveFileName = findProperty("plugin-name") + " v" + findProperty("version") + " " + "unshaded" + ".jar"
}

def targetJavaVersion = 11
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.deprecation = true
    options.encoding = 'UTF-8'
}

compileJava.options.encoding = 'UTF-8'
compileJava.dependsOn clean

build.dependsOn shadowJar

tasks.withType(Jar) {
    destinationDirectory = file("$rootDir/bin/")
}

clean.doLast {
    file("${rootDir}/bin").deleteDir()
}

def templateSource = file('src/main/templates')
def templateDest = layout.buildDirectory.dir('generated/sources/templates')
def generateTemplates = tasks.register('generateTemplates', Copy) { task ->
    def props = [
            'version': project.version
    ]
    task.inputs.properties props

    task.from templateSource
    task.into templateDest
    task.expand props
}
sourceSets.main.java.srcDir(generateTemplates.map { it.outputs })
rootProject.idea.project.settings.taskTriggers.afterSync generateTemplates